apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

repositories {
    mavenCentral()
    google()
    maven { url 'https://github.com/christoffer/readmill-java-wrapper/raw/master/snapshots' }
}

android {
    compileSdkVersion 25
    buildToolsVersion '27.0.3'

    testOptions {
        unitTests.returnDefaultValues = true
    }

    signingConfigs {
        release {
            /*
               Values below should be defined in: ~/.gradle/gradle.properties
               READTRACKER_RELEASE_STORE_FILE={path to your keystore}
               READTRACKER_RELEASE_STORE_PASSWORD=*****
               READTRACKER_RELEASE_KEY_ALIAS=*****
               READTRACKER_RELEASE_KEY_PASSWORD=*****
             */

            // Let the presences of READTRACKER_RELEASE_STORE_FILE serve as an indicator that
            // gradle.properties is setup properly.
            if(project.hasProperty('READTRACKER_RELEASE_STORE_FILE')) {
                storeFile file(READTRACKER_RELEASE_STORE_FILE)
                storePassword READTRACKER_RELEASE_STORE_PASSWORD
                keyAlias READTRACKER_RELEASE_KEY_ALIAS
                keyPassword READTRACKER_RELEASE_KEY_PASSWORD
            } else {
                throw new RuntimeException("Gradle configuration not properly setup for signing. " +
                        "See app/build.gradle for more info")
            }
        }
    }

    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 25
        versionCode 18
        versionName "3.2"
        testApplicationId "com.readtracker.android.test"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions.annotationProcessorOptions.includeCompileClasspath = true // FIXME: This should be thrown out together with ButterKnife (or at least update it to latest)
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    sourceSets {
        test {
            resources {
                srcDirs = ['src/test/assets']
            }
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:25.4.0'
    implementation 'com.android.support:support-v4:25.4.0'
    implementation 'com.j256.ormlite:ormlite-android:4.48'
    implementation 'com.j256.ormlite:ormlite-core:4.48'
    implementation 'com.squareup.picasso:picasso:2.2.0'
    implementation 'com.squareup:otto:1.3.4'
    implementation 'com.jakewharton:butterknife:4.0.1'

    testImplementation 'org.skyscreamer:jsonassert:1.2.3'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.5.1'
    testImplementation 'com.android.support.test:runner:1.0.1'
    testImplementation 'com.android.support.test:rules:1.0.1'

    androidTestImplementation 'com.android.support:support-annotations:25.4.0'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
}
