apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

repositories {
    google()
    jcenter()
    jcenter {
        url  "http://dl.bintray.com/lukaville/maven"
    }
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    testOptions {
        unitTests.returnDefaultValues = true
    }
    signingConfigs {
        release {
            /*
               Values below should be defined in: ~/.gradle/gradle.properties
               READTRACKER_RELEASE_STORE_FILE={path to your keystore}
               READTRACKER_RELEASE_STORE_PASSWORD=*****
               READTRACKER_RELEASE_KEY_ALIAS=*****
               READTRACKER_RELEASE_KEY_PASSWORD=*****
             */

            // Let the presences of READTRACKER_RELEASE_STORE_FILE serve as an indicator that
            // gradle.properties is setup properly.
            if (project.hasProperty('READTRACKER_RELEASE_STORE_FILE')) {
                storeFile file(READTRACKER_RELEASE_STORE_FILE)
                storePassword READTRACKER_RELEASE_STORE_PASSWORD
                keyAlias READTRACKER_RELEASE_KEY_ALIAS
                keyPassword READTRACKER_RELEASE_KEY_PASSWORD
            } else {
                throw new RuntimeException("Gradle configuration not properly setup for signing. " +
                        "See app/build.gradle for more info")
            }
        }
    }
    useLibrary 'org.apache.http.legacy'
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 18
        versionName "3.2"
        testApplicationId "com.readtracker.android.test"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        // FIXME: This should be thrown out together with ButterKnife (or at least update it to latest)
        javaCompileOptions.annotationProcessorOptions.includeCompileClasspath = true
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
            signingConfig signingConfigs.release
        }
    }
    lintOptions {
        abortOnError false
        disable 'DefaultLocale'
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    sourceSets {
        String sharedTestHelpers = 'src/sharedTest/src'
        String sharedTestResources = 'src/sharedTest/resources'
        test {
            java.srcDir sharedTestHelpers
            resources {
                srcDirs = [sharedTestResources]
            }
        }
        androidTest {
            java.srcDir sharedTestHelpers
            resources {
                srcDirs = [sharedTestResources]
            }
        }
    }
    productFlavors {
    }
}

dependencies {
    implementation "com.android.support:appcompat-v7:$android_support_lib_version"
    implementation "com.android.support:support-v4:$android_support_lib_version"
    implementation "com.android.support:palette-v7:$android_support_lib_version"

    // Design library is not used by ReadTracker directly, but it is used by the MaterialPicker
    // library. We include it here to have a consistent version between all the support libraries.
    implementation "com.android.support:design:$android_support_lib_version"

    implementation 'com.j256.ormlite:ormlite-android:4.48'
    implementation 'com.j256.ormlite:ormlite-core:4.48'
    implementation 'com.squareup.picasso:picasso:2.2.0'
    implementation 'com.squareup:otto:1.3.4'
    implementation 'com.jakewharton:butterknife:4.0.1'
    implementation files('lib/annotations.jar')

    // Unit test dependencies
    testImplementation 'org.skyscreamer:jsonassert:1.2.3'
    testImplementation 'junit:junit:4.12'

    // Material FilePicker
    implementation 'com.nbsp:library:1.8'

    // Required for instrumented tests
    androidTestImplementation 'org.skyscreamer:jsonassert:1.2.3'
    androidTestImplementation "com.android.support:support-annotations:$android_support_lib_version"
    androidTestImplementation('com.android.support.test:runner:1.0.2') {
        // Exclude the support annotations because they force upgrade the explicit support
        // annotation version above.
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

configurations {
    compile.exclude group: 'org.jetbrains', module: 'annotations'
}