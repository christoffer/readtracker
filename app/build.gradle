apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

repositories {
    google()
    jcenter()
    jcenter {
        url "http://dl.bintray.com/lukaville/maven"
    }
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'

    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'

    testOptions {
        unitTests.returnDefaultValues = true
    }

    signingConfigs {
        release {
            /*
               Values below should be defined in: ~/.gradle/gradle.properties
               READTRACKER_RELEASE_STORE_FILE={path to your keystore}
               READTRACKER_RELEASE_STORE_PASSWORD=*****
               READTRACKER_RELEASE_KEY_ALIAS=*****
               READTRACKER_RELEASE_KEY_PASSWORD=*****
             */

            // Let the presences of READTRACKER_RELEASE_STORE_FILE serve as an indicator that
            // gradle.properties is setup properly.
            if (project.hasProperty('READTRACKER_RELEASE_STORE_FILE')) {
                storeFile file(READTRACKER_RELEASE_STORE_FILE)
                storePassword READTRACKER_RELEASE_STORE_PASSWORD
                keyAlias READTRACKER_RELEASE_KEY_ALIAS
                keyPassword READTRACKER_RELEASE_KEY_PASSWORD
            } else {
                throw new RuntimeException("Gradle configuration not properly setup for signing. " +
                        "See app/build.gradle for more info")
            }
        }
    }

    buildFeatures {
        viewBinding true
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 20
        versionName "3.3.1"
        testApplicationId "com.readtracker.android.test"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
        disable 'DefaultLocale'
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    sourceSets {
        String sharedTestHelpers = 'src/sharedTest/src'
        String sharedTestResources = 'src/sharedTest/resources'
        test {
            java.srcDir sharedTestHelpers
            resources {
                srcDirs = [sharedTestResources]
            }
        }
        androidTest {
            java.srcDir sharedTestHelpers
            resources {
                srcDirs = [sharedTestResources]
            }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.gridlayout:gridlayout:1.0.0"
    implementation 'androidx.palette:palette:1.0.0'

    // Design library is not used by ReadTracker directly, but it is used by the MaterialPicker
    // library. We include it here to have a consistent version between all the support libraries.
    implementation 'com.google.android.material:material:1.0.0'

    implementation 'com.j256.ormlite:ormlite-android:4.48'
    implementation 'com.j256.ormlite:ormlite-core:4.48'
    implementation 'com.squareup.picasso:picasso:2.3.4'
    implementation 'com.squareup:otto:1.3.4'
    implementation files('lib/annotations.jar')
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation("com.squareup.okhttp3:okhttp:4.9.0")

    // Unit test dependencies
    testImplementation 'org.skyscreamer:jsonassert:1.2.3'
    testImplementation 'junit:junit:4.12'

    // Material FilePicker
    implementation 'com.nbsp:library:1.8'

    // Required for instrumented tests
    testImplementation 'androidx.test:core:1.3.0'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'

    androidTestImplementation 'org.skyscreamer:jsonassert:1.2.3'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

configurations {
    compile.exclude group: 'org.jetbrains', module: 'annotations'
}
